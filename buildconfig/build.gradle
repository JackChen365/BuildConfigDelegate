plugins {
    id 'groovy'
    id 'kotlin'
    id 'java-gradle-plugin'
}
version = "1.0.0"
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'build-config-delegate'
            implementationClass = 'com.android.plugin.buildconfig.BuildConfigDelegatePlugin'
        }
    }
}

configurations {
    optionalPlugins
    compileOnly.extendsFrom optionalPlugins
}
dependencies {
    optionalPlugins("com.android.tools.build:gradle:4.0.1")
    optionalPlugins("org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.10")
}
pluginUnderTestMetadata.pluginClasspath.from(configurations.optionalPlugins)

dependencies {
    implementation deps.kotlin.stdlib
    implementation 'org.ow2.asm:asm:7.0'
    implementation 'org.ow2.asm:asm-util:7.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.android.tools.build:gradle:4.0.1'
    implementation 'com.squareup:javapoet:1.13.0'
    //local repo
    testImplementation gradleTestKit()
    testImplementation 'commons-io:commons-io:2.6'
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}